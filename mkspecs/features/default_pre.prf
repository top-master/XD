# This file is loaded by qmake right before each actual project file.
# Note that evaluating variable assignments from the command line
# still happens in between these two steps.

# TRACE/mkspecs bugfix: some Qt Creator versions don't set `build_pass`,
# for example consider `!build_pass: mySlowTaskOrWarning()`.
load(qtc_run_polyfill)
qtc_run: CONFIG *= build_pass

load(exclusive_builds)
CONFIG = \
    lex yacc debug exceptions depend_includepath \
    testcase_targets import_plugins import_qpa_plugin \
    $$CONFIG

contains(QT_CONFIG, c++11):lessThan(QT_COMPILER_STDCXX, 201103): CONFIG += c++11

!build_pass:defined(QT_EDITION, var):!equals(QT_EDITION, "OpenSource"):!equals(QT_EDITION, "Preview") {
    #
    # call license checker (but cache result for one day)
    #
    today = $$section(_DATE_, " ", 0, 2)
    !isEqual(QMAKE_LICHECK_TIMESTAMP, $$today) {
        !system("$$system_quote($$system_path($$[QT_HOST_BINS/src]/$$QT_LICHECK)) check" \
                "$$QT_RELEASE_DATE $$[QMAKE_SPEC] $$[QMAKE_XSPEC]"): \
            error("License check failed! Giving up ...")

        cache(QMAKE_LICHECK_TIMESTAMP, set stash, today)
    }
    unset(today)
}

# TRACE/qmake rules: XD forbids building inside of qtbase (sub-)folder #1,
# because build artifacts may conflict, causing strange issues.
if(!build_pass|qtc_run):QTDIR_build {
    # Build dir validation.
    tmpRoot = $$clean_path($$PWD/../..)
    tmp = $$OUT_PWD
    tmp ~= s|^($$re_escape($$tmpRoot))/.*|\\1|g
    equals(tmp, "$$tmpRoot") {
        error("Build directory is not allowed to be \"$$tmpRoot\" or one of its sub-folders - re-configure the project.")
    }
    unset(tmp)
    unset(tmpRoot)
}

load(qt_functions)
load(xd_functions)
